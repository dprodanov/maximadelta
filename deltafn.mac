/*
 * (C) Dimiter Prodanov
 *
 * @version   1.2.6 Date 22 Sept 2014
		      1.2.5 Date 17 Aug 2014
		      1.2 Data 28 June 2014 
 *		      1.1 Date 29 April 2014
 *
 * @author Dimiter Prodanov
 * 		  IMEC

 * @contents
 * This library implements Diracs's delta function
 * 
 * @license This library is free software; you can redistribute it and/or
 *      modify it under the terms of the GNU Lesser General Public
 *      License as published by the Free Software Foundation; either
 *      version 2.1 of the License, or (at your option) any later version.
 *
 *      This library is distributed in the hope that it will be useful,
 *      but WITHOUT ANY WARRANTY; without even the implied warranty of
 *      MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 *       Lesser General Public License for more details.
 *
 *      You should have received a copy of the GNU Lesser General Public
 *      License along with this library; if not, write to the Free Software
 *      Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 */
 
deltainfo([_package]):=
block(
	disp("package name: deltafn.mac (c)"),
	disp("author: Dimiter Prodanov"),
	disp("major version: 1"),
	disp("minor version: 3"),
	disp("Recommended location: share/contrib"),
	disp("last update: 21 Sept 2014"),
	disp("")
)$
 
declare (delta, evenfun)$
 
_macheps():=block ( [macheps],
	macheps:1.0,
	while not equal(float(1.0 + macheps/2.0), 1.0) do (
		macheps:float(macheps/2.0)
	),
	return (macheps)
)$

numcalibrate(%fp):=(
	fpprec:%fp,
 	assume(zeroa < 1/2^fpprec),
	assume(zerob >- 1/2^fpprec)
)$

 if get('deltafn,'version)=false then ( 
	ratepsilon: _macheps(),
	fpprec:-fix(log(ratepsilon)/log(2)),
	assume(zeroa >0),
	assume(zerob <0),
	numcalibrate(fpprec)
 )$
 

 /* delta function for numenrical applications*/
_delta(x):= block(
	if abs(x)<=ratepsilon then return (1/(2*ratepsilon)) 
	else return (0) 
 )$

_unit(x):= (
	if x<0 then (0) else 
	if x>0 then (1)  
	else (1/2)
)$
 
(matchdeclare (x, atom),
	defrule (ddev1, delta(x), _delta(x)),
	defrule (ddunit, unit(x), _unit(x))
 )$
 
(matchdeclare (x, lambda([u], not(constantp(u))), c, atom),
	defrule (ddscale, delta(c*x), 1/abs(c)*delta(x))
 )$

/* shifting rules 
 */
(matchdeclare (u, lambda([x], not(constantp(x)) and atom(u)), z, atom, c, constantp),
	defrule (ddint1, 'integrate(delta(z-u), u ), unit(z)),
	defrule (ddint11, 'integrate(delta(c-u), u ), unit(c)),
	"defrule (ddint1, 'integrate(delta(z-u), u ), z)",
	"defrule (ddint10, 'integrate(delta(z+u), u ), -z)"
)$

/*
	dsubst1(f, u, c):=block ([z],
		subst(u=c, f(u))
	);
*/

/* function rules 
 */
(matchdeclare (u, lambda([u], not(constantp(u)) and atom(u)), z, atom, fn, symbolp, [ _ai, _as ], atom),
	defrule (ddint3, 'integrate(delta(z-u)*fn(u), u, minf, inf), fn(z)),
	defrule (ddint31, 'integrate(delta(z-u)*fn(u), u, _ai, _as), umatch(fn(z), _ai, _as))
)$

(matchdeclare (aa, all, xx, symbolp), 
	simp : false,
	defrule (ddint2, 'integrate(delta(xx)*aa, xx, minf, inf), subst (xx = 0, aa)),
	simp : true
)$

(matchdeclare (u, lambda([u], not(constantp(u)) and atom(u)), z, atom,  fn, symbolp, n, intvalp),
	defrule (dderiv2, 'integrate('diff(delta(u), u, n)*fn(u), u, minf, inf ), dsubst(fn, u, n) ),
	defrule (dderiv21, 'integrate('diff(delta(u), u, n)*fn(u), u, _ai, _as ), dsubst(fn, u, n)*umatch(1, _ai, _as)),
	
	dsubst(f, u, n):=block ([z],
		if not intvalp(n) then return (f),
		z:(-1)^n * diff (f(u), u, n),
		subst(u=0, z)
	),
	
	/*defrule (dderiv2, 'integrate('diff(delta(u), u, n)*fn(u), u), (-1)^n * 'integrate(delta(u)*diff (fn(u), u, n), u) ), */
	defrule (dderiv23, 'integrate('diff(delta(z-u), u, n)*fn(u), u), (-1)^n * 'integrate(delta(z-u)*diff (fn(u), u, n), u) )
)$

(matchdeclare (x, atom, n, intvalp, [ _ai, _as ], atom),
	defrule (ddint3, 'integrate(delta(x), x), unit(x)),
	defrule (ddint4, 'integrate(delta(x), x, minf, inf ), 1),
	defrule (ddint41, 'integrate(delta(x), x,  _ai, _as), umatch(1, _ai, _as)),
	defrule (dderiv1, 'diff(delta(x), x, n), 1/(-x)^n *delta(x)*factorial (n) )
)$

umatch(f, a, b):=(
	if a<0 and b>0 then f else 0
)$

intvalp(x):= integerp(x) or featurep(x, integer)$

(matchdeclare ([%darg, %deltam], true),
defmatch (ddarg, %deltam*delta (%darg))
)$

(matchdeclare (V, lambda([x], not atom (x) and not listp(x) and not constantp(x) ), u, atom, fn, symbolp),
	defrule (dderiv3, delta(V), fdroot2(V)),
	defrule (dderiv4, delta(fn(u)), fdroot(fn(u), u))
)$

fdroot(V, x):=block( [rts, n, i:1, s:0, dfn, sd, xi ],
	rts:solve(V=0,x),
	if emptyp(rts) then return (delta(V))
	else (
		n:length(rts),
		dfn:diff (V, x),
		for i:1 thru n do (
			xi:part(rts[i],2),
			sd:subst(x=xi, dfn), "print (xi, dfn, sd)",
			if (sd #0) then
				s:s+delta(x-xi)/abs(sd)
			else s: delta(V)
		),
		return (s)
	)
)$ 

fdroot2(V):=block ( [ vl: listofvars (V), v ] ,
	if length(vl)>1 then return (delta(V)),
	v:vl[1],
	return (fdroot(V, v))
)$


delta_rules : sublist (rules, lambda ([a], smismatch ("dd", sconcat (a)) = 3 and 
(a # 'ddev1 and a # 'ddunit )))$

simp_delta(x):=apply(apply1, cons('x, delta_rules))$

eval_delta(x):=apply1(x, ddev1, ddunit)$

put('deltafn, 1.3, 'version);
put('deltafn, "Dimiter Prodanov", 'author);
put('deltafn, "(C) - Dimiter Prodanov, 2014", 'copyright);